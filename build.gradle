import org.apache.tools.ant.filters.*

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.0'
    classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta1'
    classpath 'commons-httpclient:commons-httpclient:3.1'
  }
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.2'
  id 'com.github.jruby-gradle.base' version '0.1.5'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

ext {
  revealjsVersion = '3.2.0'
  asciidoctorBackendVersion = 'master'
  downloadDir = new File(buildDir,'download')
  templateDir = new File(downloadDir,'templates')
  revealjsDir  = new File(downloadDir,'reveal.js')
  highlightjs = new File(downloadDir, 'highlight.js')
}

dependencies {
  repositories {
    jcenter()
    mavenCentral()
  }

  gems 'rubygems:slim:2.1.0'
  gems 'rubygems:thread_safe:0.3.4'
  gems 'rubygems:asciidoctor-diagram:1.2.0'

  // java
  compile('org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final')
  compile('javax.validation:validation-api:1.1.0.Final')
  compile('org.projectlombok:lombok:1.16.6')
  compile('org.hibernate:hibernate-entitymanager:4.3.11.Final')
  compile('org.postgresql:postgresql:9.4.1208.jre7')
  compile('com.h2database:h2:1.4.191')
  compile('org.testng:testng:6.9.10')
  compile('org.assertj:assertj-core-java8:1.0.0m1')

}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task download << {
  mkdir downloadDir
  vfs {
    cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
    templateDir, recursive:true, overwrite:true
    cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
    revealjsDir, recursive:true, overwrite:true
    cp "http://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.2.0/highlight.min.js",
    highlightjs, recursive:true, overwrite:true
  }

  // replace highlight.js with newer version ...
  copy {
    from "${revealjsDir}/plugin/highlight/highlight.js"
    into temporaryDir
    filter(HeadFilter, lines:26)
    filter(ConcatFilter, append:highlightjs)
  }
  // ... and place it on the original path.
  copy {
    from "${temporaryDir}/highlight.js"
    into "${revealjsDir}/plugin/highlight"
  }
}

download {
  description "Download extra revealjs resources"
  outputs.dir templateDir
  outputs.dir revealjsDir
}

asciidoctor {
  dependsOn jrubyPrepareGems

  sources {
    include '*.adoc'
  }
  sourceDir 'src/docs'
  resources {
    from (sourceDir) {
      include 'images/**'
    }
    from (downloadDir) {
      include 'reveal.js/**'
    }
  }

  requires = ['asciidoctor-diagram']
  gemPath = jrubyPrepareGems.outputDir
  backends    'revealjs'

  attributes	'build-gradle': file('build.gradle'),
  'sourcedir': project.sourceSets.main.java.srcDirs[0],
  'endpoint-url': 'http://localhost',
  'source-highlighter' : 'highlightjs',
  'imagesdir':'./images',
  'toc':'left',
  'icons': 'font',
  'setanchors':'true',
  'idprefix':'',
  'idseparator':'-',
  'docinfo1':'true',
  'revealjs_theme':'night',
  'revealjs_transition':'default',
  'revealjs_history':'true',
  'revealjs_slideNumber':'true'

  options template_dirs: [new File(templateDir, 'templates/slim').absolutePath]

  dependsOn download
}

defaultTasks 'build', 'asciidoctor'
